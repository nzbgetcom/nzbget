set(YENC_ROOT ${CMAKE_BINARY_DIR}/yenc/src)
if(CMAKE_GENERATOR MATCHES "Visual Studio") 
	set(YENC_LIBS
		${YENC_ROOT}/yenc-build/rapidyenc_static/${CMAKE_BUILD_TYPE}/rapidyenc.lib
	)
elseif(CMAKE_GENERATOR MATCHES "Xcode")
	set(YENC_LIBS
		${YENC_ROOT}/yenc-build/rapidyenc_static/${CMAKE_BUILD_TYPE}/librapidyenc.a
	)
else()
	set(YENC_LIBS
		${YENC_ROOT}/yenc-build/rapidyenc_static/librapidyenc.a
	)
endif()

set(CMAKE_ARGS
	-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
	-DCMAKE_SYSTEM_PROCESSOR=${CMAKE_SYSTEM_PROCESSOR}
	-DCMAKE_SYSTEM_NAME=${CMAKE_SYSTEM_NAME}
	-DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}
)

if(DEFINED TOOLCHAIN_PREFIX)
	set(CMAKE_ARGS ${CMAKE_ARGS} -DTOOLCHAIN_PREFIX=${TOOLCHAIN_PREFIX})
endif()

if(APPLE)
	set(CMAKE_ARGS ${CMAKE_ARGS}
		-DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES}
	)
endif()

if(CMAKE_SYSROOT)
	set(CMAKE_ARGS ${CMAKE_ARGS}
		-DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}
		-DCMAKE_SYSROOT=${CMAKE_SYSROOT}
		-DCMAKE_CXX_FLAGS=-I${CMAKE_SYSROOT}/usr/include/c++/v1
	)
endif()

ExternalProject_add(
	yenc
	PREFIX			yenc
	GIT_REPOSITORY	https://github.com/animetosho/rapidyenc.git
	GIT_TAG			v1.1.1
	TLS_VERIFY		TRUE
	GIT_SHALLOW		TRUE
	GIT_PROGRESS	TRUE
	DOWNLOAD_EXTRACT_TIMESTAMP	TRUE
	BUILD_BYPRODUCTS ${YENC_LIBS}
	CMAKE_ARGS		 ${CMAKE_ARGS}
	INSTALL_COMMAND	""
)

set(LIBS ${LIBS} ${YENC_LIBS})
set(INCLUDES ${INCLUDES} ${YENC_ROOT}/yenc ${YENC_ROOT}/yenc/src)
